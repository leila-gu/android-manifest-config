// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  apply from: "config/index.gradle"

  CONFIG.versions.android.plugin = '4.2.2'

  ext.deps = [
      javapoet      : 'com.squareup:javapoet:1.10.0',
      junit         : 'junit:junit:4.12',
      compiletesting: 'com.google.testing.compile:compile-testing:0.15',
      auto          : [
          'service': 'com.google.auto.service:auto-service:1.0.1',
      ],
      kotlin      : [
          'stdLib'      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$CONFIG.versions.kotlin",
          'reflect'     : "org.jetbrains.kotlin:kotlin-reflect:$CONFIG.versions.kotlin",
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:$CONFIG.versions.kotlin"
      ],
      android       : [
          'gradlePlugin': "com.android.tools.build:gradle:$CONFIG.versions.android.plugin",
      ],
      licenserGradlePlugin: "gradle.plugin.net.minecrell:licenser:0.4.1",
      axionPlugin: 'pl.allegro.tech.build:axion-release-plugin:1.10.2'
  ]

  repositories {
    google()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
  }

  dependencies {
    //noinspection DifferentKotlinGradleVersion
    classpath deps.kotlin.gradlePlugin
    classpath deps.android.gradlePlugin
    classpath deps.licenserGradlePlugin
    classpath deps.axionPlugin
  }
}

allprojects { proj ->
  apply plugin: "net.minecrell.licenser"
  license {
    header = rootProject.file('LICENSE-HEADER.txt')
    include '**/*.java'
    include '**/*.kt'
  }

  repositories {
    google()
    mavenCentral()
  }

  apply plugin: 'pl.allegro.tech.build.axion-release'
  scmVersion {
    useHighestVersion = true
    tag {
      prefix = 'v'
      versionSeparator = ''
      versionIncrementer 'incrementMinor'
    }
  }

  proj.version = scmVersion.version

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    it.doFirst {
      kotlinOptions.freeCompilerArgs = [
              '--add-exports', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'
      ]
    }
  }
}
